archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.height.v1

concept
	[at0000]	-- Height/Length
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Lars Bitsch-Larsen">
				["organisation"] = <"University Hospital of Bergen Norway.">
			>
			accreditation = <"MD,DEAA, MBA, specialist in anesthesia, specialist in tropical medicine">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2006-03-09">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Brukes til registrering av et individs høyde/lengde fra isse til fotsåle - både målt, estimert og justert, enten stående, tilbakelent eller liggende.">
			use = <"Brukes til registrering av høyden eller lengden til et individ ved et gitt tidspunkt, også når begge bein mangler eller er forkortet på grunn av en medfødt defekt eller etter kirurgisk/traumatisk fjerning. En kommentar som identifiserer eventuelle fysiske mangler kan registreres i dataelementet \"Supplerende opplysninger\". Dette er standardarketypen som skal brukes for en vanlig måling av høyde eller lengde, uavhengig av klinisk miljø. 

Kan også brukes for registrering av estimert høyde eller lengde i en klinisk situasjon hvor det ikke er mulig å måle en nøyaktig høyde eller lengde - for eksempel måling av ikke-samarbeidsvillig barn. Dette er ikke eksplisitt modellert inn i arketypen idet openEHR referansemodellen tillater estimater for datatypen kvantitet (magnitude_status settes til \"~\"). Ved implementasjon kan det for eksempel settes kryss i en boks ved siden av datafeltet for høyde/lengde for å indikere at den registrerte høyden/lengden er et estimat. 

Ved registrering av den første lengden til et spedbarn kort tid etter fødselen, \"fødselslengde '- bruk hendelsen \"Fødsel\".

Ved registrering av justert høyde/lengde basert på målinger av andre kroppsdeler og en algoritme, f.eks en beregning av full kroppshøyde av en person som har amputert begge bein, brukes feltet \"Justert høyde/lengde\".">
			keywords = <"høydetap", "høyde", "lengde", "vekst", "høydevekst", "lengdevekst", "kroppshøyde", "kroppslengde">
			misuse = <"Brukes ikke til å registrere veksthastighet, lengden av et ikke-kroppslig objekt eller en bestemt del av kroppen.">
			copyright = <"© Nasjonal IKT HF">
		>
		["no"] = <
			language = <[ISO_639-1::no]>
			purpose = <"Brukes til registrering av et individs høyde/lengde fra isse til fotsåle - både målt, estimert og justert, enten stående, tilbakelent eller liggende.">
			use = <"Brukes til registrering av høyden eller lengden til et individ ved et gitt tidspunkt, også når begge bein mangler eller er forkortet på grunn av en medfødt defekt eller etter kirurgisk/traumatisk fjerning. En kommentar som identifiserer eventuelle fysiske mangler kan registreres i dataelementet \"Supplerende opplysninger\". Dette er standardarketypen som skal brukes for en vanlig måling av høyde eller lengde, uavhengig av klinisk miljø. 

Kan også brukes for registrering av estimert høyde eller lengde i en klinisk situasjon hvor det ikke er mulig å måle en nøyaktig høyde eller lengde - for eksempel måling av ikke-samarbeidsvillig barn. Dette er ikke eksplisitt modellert inn i arketypen idet openEHR referansemodellen tillater estimater for datatypen kvantitet (magnitude_status settes til \"~\"). Ved implementasjon kan det for eksempel settes kryss i en boks ved siden av datafeltet for høyde/lengde for å indikere at den registrerte høyden/lengden er et estimat. 

Ved registrering av den første lengden til et spedbarn kort tid etter fødselen, \"fødselslengde '- bruk hendelsen \"Fødsel\".

Ved registrering av justert høyde/lengde basert på målinger av andre kroppsdeler og en algoritme, f.eks en beregning av full kroppshøyde av en person som har amputert begge bein, brukes feltet \"Justert høyde/lengde\".">
			keywords = <"høydetap", "høyde", "lengde", "vekst", "høydevekst", "lengdevekst", "kroppshøyde", "kroppslengde">
			misuse = <"Brukes ikke til å registrere veksthastighet, lengden av et ikke-kroppslig objekt eller en bestemt del av kroppen.">
			copyright = <"© Nasjonal IKT HF">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the length of the body from crown of head to sole of foot of an individual - both actual, approximate and adjusted, and either in a standing or recumbent position.">
			use = <"To be used for recording the actual height or body length of an individual at any point in time, also if both legs are missing or shortened due to a congenital defect or surgical/traumatic removal. A statement identifying the physical incompleteness of the body can be recorded in the 'Confounding factors' protocol element, if required. This is the usual archetype to be used for a typical measurement of height or body length, independent of the clinical setting.
Can also be used for recording an approximation of height or body length measurement in a clinical scenario where it is not possible to measure an accurate height or length - for example, measuring an uncooperative child. This is not modelled explicitly in the archetype as the openEHR Reference model allows the attribute of Approximation for any Quantity data type (magnitude_status is set to \"~\"). At implementation, for example, an application user interface could allow clinicians to select an appropriately labelled check box adjacent to the Height data field to indicate that the recorded height is an approximation, rather than a measurement.

When recording the first length of a newborn infant - birth length - use the event \"Birth\".

In general, length measurements are recommended for children under 2 years of age and individuals who cannot stand; height measurements for all others.
Ideally, height is measured standing on both feet with weight distributed evenly, heels together and both buttocks and heels in contact with a vertical back board; body length is measured in a fully extended, supine position with the pelvis flat, legs extended and feet flexed. 
Use to record growth and loss of height. This can currently be modelled by constraining the 'any event' to an interval in a template with associated mathematical function of increase or decrease, as appropriate.

When recording the adjusted height/length based on measurements of other body parts and an algorithm, for example a calculation of the full body height of an individual who've had both legs amputated, use the element \"Adjusted height/length\".">
			keywords = <"shrinkage", "increase", "decrease", "height loss", "height", "length", "growth">
			misuse = <"Not to be used to record growth velocity, the length of an object or specific body part.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"Published">
	other_contributors = <"Karin Aarsheim, Helse Førde, Norway", "Grethe Almenning, Bergen kommune, Norway", "Tomas Alme, DIPS ASA, Norway", "Anne Pauline Anderssen, Helse Nord RHF, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Silje Ljosland Bakke, Helse Bergen HF, Norway (Editor)", "Lars Bitsch-Larsen, Haukeland University Hospital, Bergen, Norway", "Marja Buur, Medisch Centrum Alkmaar, Netherlands", "Rong Chen, Cambio Healthcare Systems, Sweden", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Hans Demski, Helmholtz Zentrum München, Germany", "Paul Donaldson, Nursing Informatics Australia, Australia", "Torsten Eken, Oslo universitetssykehus HF, Ullevål, Norway", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Sebastian Garde, Ocean Informatics, Germany", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore", "Anneke Goossen, Results 4 Care, Netherlands", "Gyri Gradek, Senter for medisinsk genetikk og molekylærmedisin, Haukeland Universitetssykehus, Norway", "Heather Grain, Llewelyn Informatics, Australia", "Anne Harbison, Australia", "Knut Harboe, Stavanger Universitetssjukehus, Norway", "Ingrid Heitmann, Oslo universitetssykehus HF, Norway", "Andreas Hering, Helse Bergen HF, Haukeland universitetssjukehus, Norway", "Omer Hotomaroglu, Turkey", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Tom Jarl Jakobsen, Helse Bergen, Norway", "Andrew James, University of Toronto, Canada", "Pétur Júlíusson, Barneklinikken, HUS og K2, UIB, Norway", "Karl Trygve Kalleberg, Oslo Universitetssykehus, Norway", "Sabine Leh, Helse-Bergen, Norway", "Heather Leslie, Ocean Informatics, Australia", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Camilla Lund, Institute for Cancer Genetics and Informatics, Norway", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "Ian McNicoll, Ocean Informatics, United Kingdom", "Lars Ivar Mehlum, Helse Bergen HF, Norway", "Jeroen Meintjen, Medisch Centrum Alkmaar, Netherlands", "Bjørn Næss, DIPS ASA, Norway", "Tilde Ostborg, SUS, Norway", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Thomas Schopf, University Hospital of North-Norway, Norway", "Thilo Schuler, Germany", "Kari Sygnestveit, Helse Bergen, Norway", "Line Sæle, Helse Vest IKT, Norway", "Arne Løberg Sæter, DIPS ASA, Norway", "Jan Inge Sørheim, Helse Bergen, Haukeland uniersitetssjukehus, Norway", "Micaela Thierley, Helse Bergen, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Editor)">
	other_details = <
		["references"] = <"Wilks Z, Bryan S, Mead V and Davies EH. Clinical guideline: Height, measuring a child [Internet]. London, United Kingdom: UCL Institute of Child Health; 2008 Apr 01 [cited 2009 Jul 28 ]. Available from: http://www.ich.ucl.ac.uk/clinical_information/clinical_guidelines/cpg_guideline_00060">
		["MD5-CAM-1.0.1"] = <"C4DAD5882F7A73970CA94F3298FC4977">
	>

definition
	OBSERVATION[at0000] matches {	-- Høyde/Lengde
		data matches {
			HISTORY[at0001] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {1..*} matches {	-- Uspesifisert hendelse
						data matches {
							ITEM_TREE[at0003] matches {	-- Simple
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Høyde/lengde
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::122]>
												list = <
													["1"] = <
														units = <"cm">
														magnitude = <|0.0..500.0|>
													>
												>
											>
										}
									}
									ELEMENT[at0026] occurrences matches {0..1} matches {	-- Justert høyde/lengde
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::122]>
												list = <
													["1"] = <
														units = <"cm">
														magnitude = <|0.0..500.0|>
													>
												>
											>
										}
									}
									ELEMENT[at0018] occurrences matches {0..1} matches {	-- Kommentar
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0013] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0014] occurrences matches {0..1} matches {	-- Kroppsstilling
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0016, 	-- Stående
													at0020]	-- Liggende
												}
											}
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {	-- Konfunderende faktorer
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					POINT_EVENT[at0021] occurrences matches {0..1} matches {	-- Fødsel
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]	-- /data[history]/events[Uspesifisert hendelse]/data[Simple]
						}
						state matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/state[at0013]	-- /data[history]/events[Uspesifisert hendelse]/state[Tree]
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {	-- List
				items cardinality matches {0..*; ordered} matches {
					allow_archetype CLUSTER[at0011] occurrences matches {0..1} matches {	-- Måleutstyr
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
					ELEMENT[at0025] occurrences matches {0..1} matches {	-- Justeringsmetode
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0028] occurrences matches {0..1} matches {	-- Begrunnelse for justering
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Høyde/Lengde">
					description = <"Individets høyde eller lengde målt fra isse til fotsåle. Høyde måles stående og lengde liggende.">
				>
				["at0001"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en template eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Høyde/lengde">
					description = <"Høyde/lengde fra isse til fotsåle.">
				>
				["at0007"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Måleutstyr">
					description = <"Beskrivelse av måleutstyret brukt til måling av høyde eller lengde.">
				>
				["at0013"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Kroppsstilling">
					description = <"Individets kroppsstilling på måletidspunktet.">
				>
				["at0016"] = <
					text = <"Stående">
					description = <"Høyde måles optimalt uten sko, uten sokker (for å se fotstillingen skikkelig), rumpen inntil veggen, skuldrene inntil veggen, hodet i Frankfurt stilling (nedre orbitalkant horisontalt med ytre øregang). En skal si til vedkommende \"stå rett\", men ikke skyve opp hodet.">
				>
				["at0018"] = <
					text = <"Kommentar">
					description = <"Ytterligere beskrivelse av målingen av høyde/lengde som ikke dekkes i andre felt.">
				>
				["at0019"] = <
					text = <"Konfunderende faktorer">
					description = <"Registrering av faktorer som kan ha innflytelse på måling av kroppslengden f.eks. notat om amputasjon, lue, håroppsett, klemmer eller lignende.">
				>
				["at0020"] = <
					text = <"Liggende">
					description = <"Lengde måles i en helt utstrakt, liggende posisjon med bekkenet flatt, beina strekte og føtter verken flektert eller ekstendert.">
				>
				["at0021"] = <
					text = <"Fødsel">
					description = <"Den første lengden målt etter fødselen. Denne hendelsen skal kun benyttes én gang per journal.">
				>
				["at0025"] = <
					text = <"Justeringsmetode">
					description = <"Matematisk formel for hvordan høyde/lengde registrert i feltet \"Justert høyde/lengde\" er beregnet.">
				>
				["at0026"] = <
					text = <"Justert høyde/lengde">
					description = <"Justert høyde/lengde er beregnet basert på en målt høyde/lengde, armspenn, underarmslengde eller lignende. Justert høyde/lengde brukes til bl.a. beregning av BMI hvis pasienten mangler kroppsdeler som signifikant påvirker målt høyde/lengde.">
				>
				["at0028"] = <
					text = <"Begrunnelse for justering">
					description = <"Begrunnelse for hvorfor feltet \"Justert høyde/lengde\" er benyttet.">
				>
			>
		>
		["no"] = <
			items = <
				["at0000"] = <
					text = <"Høyde/Lengde">
					description = <"Individets høyde eller lengde målt fra isse til fotsåle. Høyde måles stående og lengde liggende.">
				>
				["at0001"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en template eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Høyde/lengde">
					description = <"Høyde/lengde fra isse til fotsåle.">
				>
				["at0007"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Måleutstyr">
					description = <"Beskrivelse av måleutstyret brukt til måling av høyde eller lengde.">
				>
				["at0013"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Kroppsstilling">
					description = <"Individets kroppsstilling på måletidspunktet.">
				>
				["at0016"] = <
					text = <"Stående">
					description = <"Høyde måles optimalt uten sko, uten sokker (for å se fotstillingen skikkelig), rumpen inntil veggen, skuldrene inntil veggen, hodet i Frankfurt stilling (nedre orbitalkant horisontalt med ytre øregang). En skal si til vedkommende \"stå rett\", men ikke skyve opp hodet.">
				>
				["at0018"] = <
					text = <"Kommentar">
					description = <"Ytterligere beskrivelse av målingen av høyde/lengde som ikke dekkes i andre felt.">
				>
				["at0019"] = <
					text = <"Konfunderende faktorer">
					description = <"Registrering av faktorer som kan ha innflytelse på måling av kroppslengden f.eks. notat om amputasjon, lue, håroppsett, klemmer eller lignende.">
				>
				["at0020"] = <
					text = <"Liggende">
					description = <"Lengde måles i en helt utstrakt, liggende posisjon med bekkenet flatt, beina strekte og føtter verken flektert eller ekstendert.">
				>
				["at0021"] = <
					text = <"Fødsel">
					description = <"Den første lengden målt etter fødselen. Denne hendelsen skal kun benyttes én gang per journal.">
				>
				["at0025"] = <
					text = <"Justeringsmetode">
					description = <"Matematisk formel for hvordan høyde/lengde registrert i feltet \"Justert høyde/lengde\" er beregnet.">
				>
				["at0026"] = <
					text = <"Justert høyde/lengde">
					description = <"Justert høyde/lengde er beregnet basert på en målt høyde/lengde, armspenn, underarmslengde eller lignende. Justert høyde/lengde brukes til bl.a. beregning av BMI hvis pasienten mangler kroppsdeler som signifikant påvirker målt høyde/lengde.">
				>
				["at0028"] = <
					text = <"Begrunnelse for justering">
					description = <"Begrunnelse for hvorfor feltet \"Justert høyde/lengde\" er benyttet.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Height/Length">
					description = <"The height or length of the individual, measured from crown of head to sole of foot. Height is measured with the individual in a standing position and body length in a recumbent position.">
				>
				["at0001"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Height/Length">
					description = <"The length of the body from crown of head to sole of foot.">
				>
				["at0007"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0011"] = <
					text = <"Device">
					description = <"Description of the device used to measure height or body length.">
				>
				["at0013"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0014"] = <
					text = <"Position">
					description = <"Position of individual when measured.">
				>
				["at0016"] = <
					text = <"Standing">
					description = <"Height is optimally measured without shoes, without socks (to be able to properly assess the position of the foot), buttocks, heels and shoulders in contact with the wall, the head in the Frankfurt position (lower orbital rim horizontal with the external acoustic meatus). The individual should be told to \"stand straight\", but not to push their head up.">
				>
				["at0018"] = <
					text = <"Comment">
					description = <"Comment about the measurement of body height/length.">
				>
				["at0019"] = <
					text = <"Confounding factors">
					description = <"Record any issues or factors that may impact on the measurement of body height/length eg noting of amputation, hat, hairdo, etc.">
				>
				["at0020"] = <
					text = <"Lying">
					description = <"Length is measured in a fully extended, recumbent position with the pelvis flat, legs extended and feet flexed.">
				>
				["at0021"] = <
					text = <"Birth">
					description = <"The first length measured after birth. This event should only be used once per record.">
				>
				["at0025"] = <
					text = <"Adjustment method">
					description = <"Formula used to calculate the Adjusted height/length.">
				>
				["at0026"] = <
					text = <"Adjusted height/length">
					description = <"Adjusted height/length is calculated based on a measured height/length, armspan, underarm length, etc. Adjused height/length is used among other things to calculate BMI of patients with missing, injured or malformed body parts, which significantly affects measured height/length.">
				>
				["at0028"] = <
					text = <"Justification for adjustment">
					description = <"Justification for why the element \"Adjusted height/length\" is used.">
				>
			>
		>
	>
